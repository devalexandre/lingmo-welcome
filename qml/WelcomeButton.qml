/*
 * Copyright (C) 2026 LingmoOS Team.
 *
 * Author:     devalexandre <alexandre@dev2learn.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import QtQuick
import QtQuick.Controls
import LingmoUI.CompatibleModule 3.0 as LingmoUI

// Rounded push button: `primary` uses the accent color, `flat` has no background until hovered
Button {
    id: control

    property bool primary: false
    property real fontScale: 1.0

    implicitHeight: 36
    leftPadding: LingmoUI.Units.largeSpacing * 1.6
    rightPadding: LingmoUI.Units.largeSpacing * 1.6
    opacity: enabled ? 1.0 : 0.5
    hoverEnabled: true

    contentItem: Label {
        text: control.text
        font.pointSize: LingmoUI.Theme.fontSize * control.fontScale
        font.weight: control.primary ? Font.DemiBold : Font.Normal
        color: control.primary ? LingmoUI.Theme.highlightedTextColor
                               : control.flat ? LingmoUI.Theme.disabledTextColor : LingmoUI.Theme.textColor
        horizontalAlignment: Text.AlignHCenter
        verticalAlignment: Text.AlignVCenter
        elide: Text.ElideRight
    }

    background: Rectangle {
        radius: height / 2
        color: {
            if (control.flat)
                return control.hovered ? LingmoUI.Theme.alternateBackgroundColor : "transparent"
            const base = control.primary ? LingmoUI.Theme.highlightColor : LingmoUI.Theme.alternateBackgroundColor
            if (control.pressed)
                return Qt.darker(base, 1.15)
            if (control.hovered)
                return control.primary ? Qt.lighter(base, 1.1) : Qt.darker(base, LingmoUI.Theme.darkMode ? 0.85 : 1.05)
            return base
        }
        border.width: control.primary || control.flat ? 0 : 1
        border.color: LingmoUI.Theme.darkMode ? Qt.rgba(1, 1, 1, 0.08) : Qt.rgba(0, 0, 0, 0.08)

        Behavior on color { ColorAnimation { duration: 120 } }
    }
}
